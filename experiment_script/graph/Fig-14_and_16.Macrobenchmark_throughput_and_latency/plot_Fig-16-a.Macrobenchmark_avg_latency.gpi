#kiops
set terminal postscript eps enhanced color dashed dashlength 3 \
	                linewidth 2 rounded font "Helvetica, 38"

fn(v) = sprintf("%.1f", v)
fnn(v) = sprintf("%d", v)
max_(x, y) = (x>y)?x:y
max_4(w, x, y, z) = max_(w, max_( max_(x, y), z) ) 

#set output "./graph_fio_cmp_kiops.eps"
set output "./Fig-16-a.Macrobenchmark_avg_latency.eps"
set term postscript size 7.5, 3.0
set key outside top horizontal reverse Left font "Helvetica, 32"

set key samplen 2 width 1

set xtics nomirror
set ytics nomirror

set xtics font "Helvetica, 38" offset 0, 0.3 

set xrange[0.5:6.5]
set yrange[0:1.3]
set key at 5.5, 1.38
set ytics 0.5 font "Helvetica, 38" offset 0.5, 0

set ylabel font "Helvetica, 38" "Norm. Latency" offset 3, -0.5

set style data histogram
set style fill solid 1.0 border 0
set style histogram clustered gap 1
set boxwidth 1.0 absolute

set border 11

h = 0.13; gap = 0.03

#l = 0.15; r = 0.823; rr = 0.99; m = (l+r)/2; mm = (r+rr)/2;

#ll = 0.10; l = 0.25; r = 0.823; rr = 0.99; m = (l+r)/2; mm = (r+rr)/2;

#gap_ = 0.15;
#l = ll + gap_;
#r = ll + 4*gap_;
#rr = 0.99; m = (l+r)/2; mm = (r+rr)/2;
#lm = (ll+l) / 2;

#fileserver - ycsb - tpcc
#ll = 0.1; rr = 0.99; gap_ = (rr-ll)/6
#l = ll+gap_; r = l+4*gap_; m = (l+r)/2; mm = (r+rr)/2;

#fileserver - tpcc - ycsb
ll = 0.09; rr = 0.99; gap_ = (rr-ll)/6
l = ll+gap_; 
r=rr-gap_;
m=(l+r)/2;
m_update=(l+m)/2
m_read=(m+r)/2



set lmargin at screen ll
set rmargin at screen rr
set bmargin at screen 0.3
set tmargin at screen 0.88

# 가로선
set arrow from screen ll, screen h to screen rr, screen h nohead

# 세로선
set arrow from screen ll, screen h-gap to screen ll, screen h+gap nohead
set arrow from screen l, screen h-gap to screen l, screen h+gap nohead
set arrow from screen m, screen h-gap to screen m, screen h+gap nohead
set arrow from screen r, screen h-gap to screen r, screen h+gap nohead
set arrow from screen rr, screen h-gap to screen rr, screen h+gap nohead

# TPC-C
set object 1 rectangle behind at screen (l+ll)/2, screen h size char strlen("Tx"), \
	 char 0.3 front fillcolor rgb "white" fillstyle solid noborder
set label "Tx\n(sec)" at screen (l+ll)/2, screen h center front font "Helvetica, 38"


# YCSB Update
set object 2 rectangle behind at screen m_update, screen h size char strlen("Update"), \
	 char 0.3 front fillcolor rgb "white" fillstyle solid noborder
set label "Update\n(msec)" at screen m_update, screen h center front font "Helvetica, 38"

# YCSB Read
set object 3 rectangle behind at screen m_read, screen h size char strlen("Read"), \
         char 0.3 front fillcolor rgb "white" fillstyle solid noborder
set label "Read\n(msec)" at screen m_read, screen h center front font "Helvetica, 38"

# Fileserver
set object 4 rectangle behind at screen (r+rr)/2, screen h size char strlen("WrtFil"), \
         char 0.3 front fillcolor rgb "white" fillstyle solid noborder
set label "WrtFile\n(msec)" at screen (r+rr)/2, screen h center front font "Helvetica, 38"


unset key

plot 'avg_lat'\
		  u ($3/max_4($2, $3, $4, $5)):xtic(1) t 'D2FS' lc 'blue', \
		 '' u ($2/max_4($2, $3, $4, $5)):xtic(1) t 'IPLFS' lc 'red', \
		 '' u ($4/max_4($2, $3, $4, $5)):xtic(1) t 'zF2FS' lc 'dark-violet', \
		 '' u ($5/max_4($2, $3, $4, $5)):xtic(1) t 'F2FS' lc 'black', \
                '' u ($0-0.1):($2/max_4($2, $3, $4, $5)-0.05):(fn(($2)))  with labels rotate by 90 font "Helvetica, 38" offset 0,1.4 notitle, \
                 '' u ($0-0.3):($3/max_4($2, $3, $4, $5)-0.05):(fn(($3)))  with labels rotate by 90 font "Helvetica, 38" offset 0,1.4 notitle, \
                 '' u ($0+0.1):($4/max_4($2, $3, $4, $5)-0.05):(fn(($4)))  with labels rotate by 90 font "Helvetica, 38" offset 0,1.6 notitle, \
                 '' u ($0+0.3):($5/max_4($2, $3, $4, $5)-0.05):(fn(($5)))  with labels rotate by 90 font "Helvetica, 38" offset 0,1.4 notitle \
